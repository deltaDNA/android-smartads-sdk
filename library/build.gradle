/*
 * Copyright (c) 2016 deltaDNA Ltd. All rights reserved.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        
        versionCode 1
        versionName VERSION_NAME
        
        buildConfigField('String', logTagName, "\"$logTagValue\"")
        
        archivesBaseName = "${POM_ARTIFACT_ID}-${versionName}.${System.getenv("BUILD_NUMBER") ?: getSha()}"
        
        consumerProguardFiles 'proguard.cfg'
    }
    
    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
    }
    
    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }
    
    testOptions {
        unitTests.returnDefaultValues = true
    }
    
    lintOptions {
        // workaround for aar build, which reports missing stax dependency.
        disable 'InvalidPackage'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile project(':library-core')
    
    compile 'com.deltadna.android:deltadna-sdk:4.1.0-SNAPSHOT'
    
    testCompile "com.google.truth:truth:$truthVersion"
    testCompile "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testCompile "org.json:json:$jsonVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
}

apply from: "$rootProject.projectDir/gradle/mvn-push.gradle"

// https://github.com/dam5s/android-example-kotlin/issues/1
buildscript {
    repositories {
        jcenter()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}
