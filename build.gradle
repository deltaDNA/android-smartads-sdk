/*
 * Copyright (c) 2016 deltaDNA Ltd. All rights reserved.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext {
        kotlinVersion = '1.2.10'
        multidexVersion = '1.0.1'
        playServicesVersion = '11.8.0'
        supportVersion = '26.1.0'
        
        logTagName  = 'LOG_TAG'
        logTagValue = 'deltaDNA'
    }
    
    repositories {
        jcenter()
        google()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.2'
        classpath 'gradle.plugin.com.nimbledroid:gradle-profiler:1.1.8'
        classpath 'org.jacoco:org.jacoco.core:0.7.9'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url 'http://deltadna.bintray.com/android' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://raw.githubusercontent.com/HyprMXMobile/Android-SDKs/master' }
        maven { url 'https://s3.amazonaws.com/moat-sdk-builds' }
        maven { url 'https://tapjoy.bintray.com/maven' }
    }
}

subprojects {
    def isProvider = it.name.startsWith('provider')
    def isLibrary = it.name.startsWith('library') || isProvider
    
    if (isLibrary) {
        apply plugin: 'com.android.library'
        apply plugin: 'jacoco-android'
    } else {
        apply plugin: 'com.android.application'
        apply plugin: 'com.nimbledroid.profiler'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    
    if (isLibrary) {
        // workaround for group/version not picked up through project() dependency
        group = GROUP
        version = VERSION_NAME
    }
    
    android {
        compileSdkVersion 26
        buildToolsVersion '26.0.3'
        
        defaultConfig {
            minSdkVersion 16
            targetSdkVersion 26
            
            versionCode 1
            versionName VERSION_NAME
            
            if (isProvider) {
                buildConfigField('String', logTagName, "\"$logTagValue $PROVIDER_NAME\"")
                
                buildConfigField('String', 'PROVIDER_NAME', "\"${PROVIDER_NAME.toUpperCase()}\"")
                buildConfigField('String', 'PROVIDER_VERSION', "\"$PROVIDER_VERSION\"")
            } else {
                buildConfigField('String', logTagName, "\"$logTagValue\"")
            }
            
            if (isLibrary) {
                archivesBaseName = "${POM_ARTIFACT_ID}-${versionName}.${System.getenv("BUILD_NUMBER") ?: getSha()}"
                
                consumerProguardFiles 'proguard.cfg'
            }
        }
        
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
        
        if (isLibrary) {
            sourceSets {
                test.java.srcDirs += 'src/test/kotlin'
            }
            
            testOptions {
                unitTests.returnDefaultValues = true
            }
            
            buildTypes {
                debug {
                    testCoverageEnabled true
                }
            }
        }
    }
    
    if (isLibrary) {
        dependencies {
            testImplementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
            testImplementation 'com.google.truth:truth:0.37'
            testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
            testImplementation 'junit:junit:4.12'
            testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
            testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
            testImplementation 'org.json:json:20171018'
            testImplementation 'org.mockito:mockito-core:2.13.0'
            testImplementation 'org.robolectric:robolectric:3.6.1'
        }
    }
    
    if (!isLibrary && project.hasProperty('nimbleDroidApiKey')) {
        nimbledroid {
            apiKey nimbleDroidApiKey
        }
    }
    
    if (isLibrary) {
        apply from: "$rootProject.projectDir/gradle/mvn-push.gradle"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

static def getSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}
